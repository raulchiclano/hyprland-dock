import QtQuick
import "DockStyle.js" as DockStyle
import "Applications.js" as Applications
import "WindowPolicy.js" as WindowPolicy
import Quickshell.Hyprland
import Quickshell
import Quickshell.Wayland
import Quickshell.Widgets

Item {
  id: root

  required property string desktopId
  required property int itemIndex
  required property int slotSize
  required property int iconSize
  required property real magnification
  required property real magnificationRadius
  required property real pointerPosition
  required property string clickAction
  required property bool autoHide
  required property string position
  required property bool vertical
  property bool isPinned: true
  property var runningApplication: null
  property real reorderOffset: 0
  signal dragStarted(int itemIndex)
  signal dragMoved(real mainPosition)
  signal dragFinished()
  signal addApplicationRequested()
  signal removeRequested(string desktopId)
  signal pinRequested(string desktopId)
  signal autoHideToggled(bool enabled)
  signal contextMenuVisibilityChanged(bool visible)

  // Reading the model makes this binding update when Quickshell finishes its
  // asynchronous desktop-entry scan. Calling byId() alone is not reactive.
  readonly property var applications: DesktopEntries.applications.values || []
  readonly property var entry: {
    var modelRevision = applications.length
    return runningApplication ? runningApplication.entry : desktopId ? DesktopEntries.byId(desktopId) : null
  }
  readonly property var toplevels: ToplevelManager.toplevels.values || []
  readonly property var matchingWindows: {
    if (runningApplication) return runningApplication.windows.filter(w => w && toplevels.indexOf(w) >= 0)
    return toplevels.filter(w => Applications.score(w, desktopId, entry) > 0)
  }
  readonly property var runningToplevel: matchingWindows.length ? matchingWindows[0] : null
  // Use the workspace the user is working in, including an open scratchpad.
  readonly property var focusedMonitor: Hyprland.focusedMonitor
  readonly property int currentWorkspace: {
    if (!focusedMonitor) return 0
    var special = focusedMonitor.lastIpcObject.specialWorkspace
    return special && special.id ? special.id
      : Hyprland.focusedWorkspace ? Hyprland.focusedWorkspace.id : 0
  }
  readonly property var windowSelection: WindowPolicy.select(matchingWindows,
    Hyprland.toplevels.values, currentWorkspace, focusedMonitor ? focusedMonitor.id : -1)
  readonly property var localTarget: windowSelection.local.length ? windowSelection.local[0] : null
  readonly property var localWindow: localTarget ? localTarget.window : null
  readonly property var launchPlan: WindowPolicy.launchPlan(entry, matchingWindows.length > 0)
  readonly property bool canLaunchHere: currentWorkspace !== 0 && launchPlan.kind !== "blocked"
  readonly property real dragOffset: dragHandler.active
    ? vertical ? dragHandler.activeTranslation.y : dragHandler.activeTranslation.x
    : 0
  readonly property real itemCenter: (vertical ? y + height / 2 : x + width / 2)
    + dragOffset + reorderOffset
  readonly property real distance: Math.abs(pointerPosition - itemCenter)
  readonly property real influence: pointerPosition < -1000
    ? 0
    : Math.exp(-(distance * distance) / (magnificationRadius * magnificationRadius))
  readonly property real iconScale: 1 + (magnification - 1) * influence

  function launch() {
    if (!canLaunchHere) {
      contextMenu.open()
      return
    }
    if (launchPlan.kind === "action") launchPlan.action.execute()
    else if (entry) entry.execute()
  }

  function focusWindow(target) {
    if (!target) return
    var request = WindowPolicy.focusRequest(target.address, Hyprland.usingLua)
    if (request) Hyprland.dispatch(request)
  }

  function activateOrLaunch() {
    var action = WindowPolicy.intent(windowSelection.local.length,
      canLaunchHere, isPinned && clickAction !== "focus-or-launch")
    if (action === "focus") focusWindow(localTarget)
    else if (action === "launch") launch()
    else contextMenu.open()
  }

  function closeRunning() {
    // Never close a hidden window on another workspace from this action.
    if (localWindow) localWindow.close()
  }

  width: vertical ? slotSize + 6 : slotSize
  height: vertical ? slotSize : slotSize + 6
  z: dragHandler.active ? 2 : 0
  transform: Translate {
    x: root.vertical ? 0 : root.dragOffset + root.reorderOffset
    y: root.vertical ? root.dragOffset + root.reorderOffset : 0
  }

  Behavior on reorderOffset {
    NumberAnimation { duration: 120; easing.type: Easing.OutCubic }
  }

  Item {
    id: iconContainer

    x: root.vertical
      ? root.position === "left" ? 6 : root.width - root.iconSize - 6
      : (root.width - root.iconSize) / 2
    y: root.vertical
      ? (root.height - root.iconSize) / 2
      : root.position === "top" ? 6 : root.height - root.iconSize - 6
    width: root.iconSize
    height: root.iconSize
    transformOrigin: root.position === "top"
      ? Item.Top
      : root.position === "left"
        ? Item.Left
        : root.position === "right" ? Item.Right : Item.Bottom
    scale: root.iconScale

    Behavior on scale {
      NumberAnimation { duration: 90; easing.type: Easing.OutCubic }
    }

    Rectangle {
      anchors.fill: parent
      anchors.margins: -4
      radius: 14
      color: mouse.hovered ? Qt.rgba(1, 1, 1, 0.10) : "transparent"

      Behavior on color { ColorAnimation { duration: 100 } }
    }

    IconImage {
      anchors.fill: parent
      source: root.entry && root.entry.icon
        ? Quickshell.iconPath(root.entry.icon, true)
        : Quickshell.iconPath("application-x-executable", true)
      // Avoid Qt icon pixmap loading on the background image thread.
      asynchronous: false
    }

    Rectangle {
      width: 4
      height: 4
      radius: 2
      x: root.position === "left"
        ? iconContainer.width + 2
        : root.position === "right"
          ? -6
          : (iconContainer.width - width) / 2
      y: root.position === "top"
        ? -6
        : root.position === "bottom"
          ? iconContainer.height + 2
          : (iconContainer.height - height) / 2
      color: root.runningToplevel ? "#B4A1F5" : "transparent"
    }
  }

  Rectangle {
    id: tooltip

    visible: mouse.hovered && !contextMenu.visible
    x: root.position === "left"
      ? iconContainer.x + iconContainer.width + 12
      : root.position === "right"
        ? iconContainer.x - width - 12
        : (root.width - width) / 2
    y: root.position === "top"
      ? iconContainer.y + iconContainer.height + 12
      : root.position === "bottom"
        ? iconContainer.y - height - 12
        : (root.height - height) / 2
    width: tooltipText.implicitWidth + 18
    height: tooltipText.implicitHeight + 10
    radius: 8
    color: DockStyle.panel
    border.width: 1
    border.color: DockStyle.border
    z: 10

    Text {
      id: tooltipText
      anchors.centerIn: parent
      text: root.entry ? root.entry.name : root.runningApplication
        ? root.runningApplication.appId || (root.runningToplevel ? root.runningToplevel.title : "Aplicación")
        : root.desktopId
      color: DockStyle.text
      font.family: DockStyle.fontFamily
      font.pixelSize: 13
      font.weight: Font.DemiBold
    }
  }

  HoverHandler {
    id: mouse
    cursorShape: Qt.PointingHandCursor
  }

  TapHandler {
    acceptedButtons: Qt.LeftButton
    onTapped: root.activateOrLaunch()
  }

  TapHandler {
    acceptedButtons: Qt.RightButton
    onTapped: contextMenu.open()
  }

  DragHandler {
    id: dragHandler
    enabled: root.isPinned

    target: null
    acceptedButtons: Qt.LeftButton
    xAxis.enabled: !root.vertical
    yAxis.enabled: root.vertical
    onActiveChanged: {
      if (active)
        root.dragStarted(root.itemIndex)
      else
        root.dragFinished()
    }
    onActiveTranslationChanged: {
      if (active)
        root.dragMoved((root.vertical ? root.y + root.height / 2 : root.x + root.width / 2)
          + (root.vertical ? activeTranslation.y : activeTranslation.x))
    }
  }

  Component.onDestruction: {
    if (contextMenu.visible) root.contextMenuVisibilityChanged(false)
  }

  DockContextMenu {
    id: contextMenu

    anchorItem: root
    position: root.position
    isPinned: root.isPinned
    canPin: root.entry !== null && !root.entry.noDisplay
    canLaunch: root.canLaunchHere
    launchText: root.runningToplevel
      ? root.canLaunchHere ? "Nueva ventana aquí" : "Nueva ventana no disponible"
      : "Abrir aquí"
    windowSelection: root.windowSelection
    applicationName: root.entry ? root.entry.name : root.runningApplication
      ? root.runningApplication.appId || "Aplicación" : root.desktopId
    canClose: root.localWindow !== null
    onWindowChosen: address => root.focusWindow(WindowPolicy.findWindow(root.windowSelection, address))
    autoHide: root.autoHide
    onVisibleChanged: root.contextMenuVisibilityChanged(visible)
    onOpenNewWindow: root.launch()
    onCloseWindow: root.closeRunning()
    onAddApplication: root.addApplicationRequested()
    onRemoveFromDock: root.removeRequested(root.desktopId)
    onPinToDock: root.pinRequested(root.desktopId)
    onToggleAutoHide: root.autoHideToggled(!root.autoHide)
  }
}
