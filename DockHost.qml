pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Hyprland
import "components"

Item {
  id: root

  required property string configPath

  property var settings: ({
    iconSize: 42,
    magnification: 1.2,
    magnificationRadius: 95,
    margin: 10,
    backgroundOpacity: 0.88,
    position: "bottom",
    fullLength: false,
    reserveSpace: false,
    autoHide: true,
    intelligentHide: true,
    clickAction: "focus-or-launch",
    pinned: [
      "org.gnome.Nautilus",
      "zen",
      "foot",
      "code",
      "WhatsApp"
    ]
  })

  // Refresh geometry without spawning processes; Hyprland does not emit every drag step.
  Timer {
    interval: 500
    running: root.settings.autoHide === true && root.settings.intelligentHide === true
    repeat: true
    triggeredOnStart: true
    onTriggered: {
      Hyprland.refreshMonitors()
      Hyprland.refreshToplevels()
    }
  }

  // Keep focus history and special-workspace metadata fresh even without auto-hide.
  Connections {
    target: Hyprland
    function onRawEvent(event) {
      if (["activewindowv2", "workspacev2", "focusedmon", "activespecial",
           "openwindow", "closewindow", "movewindowv2", "pin"].indexOf(event.name) >= 0)
        windowRefresh.restart()
    }
  }
  Timer {
    id: windowRefresh
    interval: 40
    onTriggered: {
      Hyprland.refreshMonitors()
      Hyprland.refreshToplevels()
    }
  }

  function loadSettings(raw) {
    try {
      var parsed = JSON.parse(raw)
      if (!parsed.pinned || !Array.isArray(parsed.pinned))
        throw new Error("'pinned' must be an array")
      settings = parsed
    } catch (error) {
      console.warn("Dock: could not load " + configPath + ":", error)
    }
  }

  function reorderPinned(from, to) {
    if (from === to || from < 0 || to < 0
        || from >= settings.pinned.length || to >= settings.pinned.length)
      return

    var pinned = settings.pinned.slice()
    var moved = pinned.splice(from, 1)[0]
    pinned.splice(to, 0, moved)

    savePinned(pinned)
  }

  function pinApplication(desktopId) {
    if (!desktopId || settings.pinned.indexOf(desktopId) >= 0) return

    var pinned = settings.pinned.slice()
    pinned.push(desktopId)
    savePinned(pinned)
  }

  function unpinApplication(desktopId) {
    var index = settings.pinned.indexOf(desktopId)
    if (index < 0) return

    var pinned = settings.pinned.slice()
    pinned.splice(index, 1)
    savePinned(pinned)
  }

  function savePinned(pinned) {
    saveSetting("pinned", pinned)
  }

  function saveSetting(key, value) {
    var updated = {}
    for (var setting in settings)
      updated[setting] = settings[setting]
    updated[key] = value

    settings = updated
    configFile.setText(JSON.stringify(updated, null, 2) + "\n")
  }

  FileView {
    id: configFile

    path: root.configPath
    watchChanges: true
    printErrors: false
    blockWrites: true
    onLoaded: root.loadSettings(text())
    // FileView.text() is still stale inside onFileChanged. Reload first and
    // parse the fresh contents when onLoaded fires.
    onFileChanged: reload()
    onSaveFailed: error => console.warn("Dock: could not save " + root.configPath + ":", error)
  }

  Variants {
    model: Quickshell.screens

    delegate: Component {
      Dock {
        required property var modelData
        screen: modelData
        settings: root.settings
        onReorderRequested: (from, to) => root.reorderPinned(from, to)
        onPinRequested: desktopId => root.pinApplication(desktopId)
        onUnpinRequested: desktopId => root.unpinApplication(desktopId)
        onAutoHideRequested: enabled => root.saveSetting("autoHide", enabled)
      }
    }
  }
}
